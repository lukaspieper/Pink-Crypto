plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

android {
    namespace "de.lukaspieper.crypto.pink"
    compileSdk 33

    defaultConfig {
        minSdk 20
        targetSdk 31
        versionCode 2
        versionName "0.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.22.1"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += '-Xexplicit-api=strict'
    }
}

dependencies {
    implementation("org.signal:argon2:13.1@aar")
    implementation("com.google.crypto.tink:tink-android:1.7.0")

    // Tests
    androidTestImplementation("androidx.test.ext:junit:1.1.3")
    androidTestImplementation("androidx.test:runner:1.4.0")
    androidTestImplementation("androidx.test:rules:1.4.0")
    androidTestImplementation("com.willowtreeapps.assertk:assertk-jvm:0.25")
    androidTestImplementation("org.json:json:20220924")
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/lukaspieper/pink-crypto"
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }

        publications {
            release(MavenPublication) {
                from components.release

                groupId "de.lukaspieper.crypto.pink"
                artifactId "pink-android"
                version android.defaultConfig.versionName

                pom {
                    name = 'Pink Crypto'
                    url = 'https://github.com/lukaspieper/pink-crypto'
                    developers {
                        developer {
                            name = 'Lukas Pieper'
                        }
                    }
                    scm {
                        url = 'https://github.com/lukaspieper/pink-crypto'
                    }
                }
            }
        }
    }
}