plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

android {
    namespace "de.lukaspieper.crypto.pink"
    compileSdk 34

    defaultConfig {
        minSdk 20
        targetSdk 34
        versionCode 5
        versionName "0.5"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_TOOLCHAIN=clang'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += '-Xexplicit-api=strict'
    }
}

dependencies {
    implementation("androidx.annotation:annotation:1.7.1")
    implementation("com.google.crypto.tink:tink-android:1.13.0")

    // Tests
    androidTestImplementation("androidx.test.ext:junit:1.1.5")
    androidTestImplementation("androidx.test:runner:1.6.0")
    androidTestImplementation("androidx.test:rules:1.5.0")
    androidTestImplementation("com.willowtreeapps.assertk:assertk-jvm:0.26.1")
}

preBuild.doFirst {
    copy {
        from "../phc-winner-argon2/include"
        from "../phc-winner-argon2/src"
        into "src/main/cpp/argon2"
    }
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/lukaspieper/pink-crypto"
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }

        publications {
            release(MavenPublication) {
                from components.release

                groupId "de.lukaspieper.crypto.pink"
                artifactId "pink-android"
                version android.defaultConfig.versionName

                pom {
                    name = 'Pink Crypto'
                    url = 'https://github.com/lukaspieper/pink-crypto'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://github.com/lukaspieper/Pink-Crypto/blob/develop/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            name = 'Lukas Pieper'
                        }
                    }
                    scm {
                        url = 'https://github.com/lukaspieper/pink-crypto'
                    }
                }
            }
        }
    }
}